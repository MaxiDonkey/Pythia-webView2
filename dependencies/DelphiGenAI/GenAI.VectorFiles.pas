unit GenAI.VectorFiles;

{-------------------------------------------------------------------------------

      Github repository :  https://github.com/MaxiDonkey/DelphiGenAI
      Visit the Github repository for the documentation and use examples

 ------------------------------------------------------------------------------}

interface

uses
  System.SysUtils, System.Classes, System.Threading, System.JSON, REST.Json.Types,
  REST.JsonReflect, System.Net.URLClient,
  GenAI.API.Params, GenAI.API, GenAI.Consts, GenAI.Types, GenAI.Async.Support,
  GenAI.Async.Promise, GenAI.API.Lists, GenAI.API.Deletion, GenAI.Vector;

type
  TVectorStoreFilesUrlParams = class(TUrlAdvancedParams)
  public
    /// <summary>
    /// Filters the results of the vector store files request based on a given file status.
    /// </summary>
    /// <param name="Value">
    /// A string representing the file status to filter by. Supported values typically include
    /// statuses such as <c>in_progress</c>, <c>completed</c>, <c>failed</c>, or <c>cancelled</c>.
    /// </param>
    /// <returns>
    /// Returns the current instance of <c>TVectorStoreFilesUrlParams</c>, enabling method chaining.
    /// </returns>
    /// <remarks>
    /// This method is useful for narrowing down the results to only those files with a particular
    /// status within the vector store, improving query efficiency and clarity of results.
    /// </remarks>
    function Filter(const Value: string): TVectorStoreFilesUrlParams;
  end;

  TVectorStoreFilesCreateParams = class(TJSONParam)
  public
    /// <summary>
    /// Specifies the ID of the file to be attached to the vector store.
    /// </summary>
    /// <param name="Value">
    /// A string representing the unique identifier of the file to be added to the vector store.
    /// </param>
    /// <returns>
    /// Returns the current instance of <c>TVectorStoreFilesCreateParams</c>, enabling method chaining.
    /// </returns>
    /// <remarks>
    /// This method is essential for indicating which file should be chunked and processed
    /// when creating a new vector store entry.
    /// </remarks>
    function FileId(const Value: string): TVectorStoreFilesCreateParams;

    /// <summary>
    /// Specifies the chunking strategy to be used when processing the file.
    /// </summary>
    /// <param name="Value">
    /// An instance of <c>TChunkingStrategyParams</c> that defines settings like maximum chunk size
    /// and token overlap for dividing the file into smaller chunks.
    /// </param>
    /// <returns>
    /// Returns the current instance of <c>TVectorStoreFilesCreateParams</c>, enabling method chaining.
    /// </returns>
    /// <remarks>
    /// The chunking strategy determines how the file is split for efficient searching and retrieval
    /// within the vector store. Users can define static or auto chunking depending on their use case.
    /// </remarks>
    function ChunkingStrategy(const Value: TChunkingStrategyParams): TVectorStoreFilesCreateParams;

    /// <summary>
    /// Sets a set of key-value pairs that can be attached to the file.
    /// </summary>
    /// <param name="Value">
    /// A <c>TJSONObject</c> containing up to 16 key-value pairs. Keys are strings with a maximum length
    /// of 64 characters; values are strings (max 512 chars), booleans, or numbers.
    /// </param>
    /// <returns>
    /// Returns the current instance of <c>TVectorStoreFilesCreateParams</c>, enabling method chaining.
    /// </returns>
    /// <remarks>
    /// This can be useful for storing additional information about the file in a structured format,
    /// and querying for objects via the API or the dashboard. Used for filtering during vector store searches.
    /// </remarks>
    function Attributes(const Value: TJSONObject): TVectorStoreFilesCreateParams;
  end;

  TLastError = class
  private
    FCode: string;
    FMessage: string;
  public
    /// <summary>
    /// Gets or sets the code associated with the error.
    /// </summary>
    property Code: string read FCode write FCode;

    /// <summary>
    /// Gets or sets a human-readable message describing the error.
    /// </summary>
    property Message: string read FMessage write FMessage;
  end;

  TChunkingStrategyStatic = class
  private
    [JsonNameAttribute('max_chunk_size_tokens')]
    FMaxChunkSizeTokens: Int64;
    [JsonNameAttribute('chunk_overlap_tokens')]
    FChunkOverlapTokens: Int64;
  public
    /// <summary>
    /// Specifies the maximum number of tokens allowed in each chunk.
    /// </summary>
    /// <remarks>
    /// The default value is typically 800 tokens. The minimum value is 100, and the maximum
    /// value is 4096. This setting controls how large each chunk will be when splitting the file.
    /// </remarks>
    property MaxChunkSizeTokens: Int64 read FMaxChunkSizeTokens write FMaxChunkSizeTokens;

    /// <summary>
    /// Specifies the number of tokens that overlap between consecutive chunks.
    /// </summary>
    /// <remarks>
    /// The default value is 400 tokens. The overlap should not exceed half of
    /// <c>MaxChunkSizeTokens</c>. Overlapping tokens help preserve context between chunks
    /// and improve search accuracy within the vector store.
    /// </remarks>
    property ChunkOverlapTokens: Int64 read FChunkOverlapTokens write FChunkOverlapTokens;
  end;

  TChunkingStrategy = class
  private
    FType: string;
    FStatic: TChunkingStrategyStatic;
  public
    /// <summary>
    /// Specifies the type of chunking strategy being used.
    /// </summary>
    /// <remarks>
    /// The value of this property typically indicates the strategy type, such as <c>static</c>
    /// or <c>auto</c>. This helps determine how the chunking process is handled.
    /// </remarks>
    property &Type: string read FType write FType;

    /// <summary>
    /// Specifies the static chunking configuration for the vector store file.
    /// </summary>
    /// <remarks>
    /// This property contains settings for static chunking, including the maximum chunk size
    /// and token overlap. It is used when the chunking strategy is explicitly defined as static.
    /// </remarks>
    property Static: TChunkingStrategyStatic read FStatic write FStatic;

    destructor Destroy; override;
  end;

  TVectorStoreFile = class(TJSONFingerprint)
  private
    FId: string;
    FObject: string;
    [JsonNameAttribute('usage_bytes')]
    FUsageBytes: Int64;
    [JsonNameAttribute('created_at')]
    FCreatedAt: Int64;
    [JsonNameAttribute('vector_store_id')]
    FVectorStoreId: string;
    [JsonReflectAttribute(ctString, rtString, TRunStatusInterceptor)]
    FStatus: TRunStatus;
    [JsonNameAttribute('last_error')]
    FLastError: TLastError;
    [JsonNameAttribute('chunking_strategy')]
    FChunkingStrategy: TChunkingStrategy;
    FAttributes: string;
  private
    function GetCreatedAt: Int64;
    function GetCreatedAtAsString: string;
  public
    /// <summary>
    /// Gets or sets the unique identifier of the vector store file.
    /// </summary>
    property Id: string read FId write FId;

    /// <summary>
    /// Gets or sets the object type, which is always <c>vector_store.file</c>.
    /// </summary>
    property &Object: string read FObject write FObject;

    /// <summary>
    /// Gets or sets the total usage of the file in bytes within the vector store.
    /// </summary>
    /// <remarks>
    /// This value represents the amount of storage the file consumes after being chunked and indexed.
    /// </remarks>
    property UsageBytes: Int64 read FUsageBytes write FUsageBytes;

    /// <summary>
    /// Gets or sets the Unix timestamp (in seconds) indicating when the file was added to the vector store.
    /// </summary>
    property CreatedAt: Int64 read GetCreatedAt;

    /// <summary>
    /// Gets the human-readable representation of the creation timestamp.
    /// </summary>
    property CreatedAtAsString: string read GetCreatedAtAsString;

    /// <summary>
    /// Gets or sets the identifier of the vector store that the file belongs to.
    /// </summary>
    property VectorStoreId: string read FVectorStoreId write FVectorStoreId;

    /// <summary>
    /// Gets or sets the current processing status of the vector store file.
    /// </summary>
    /// <remarks>
    /// The status can be one of the following: <c>in_progress</c>, <c>completed</c>, <c>failed</c>,
    /// or <c>cancelled</c>. This status helps in monitoring the file's processing progress.
    /// </remarks>
    property Status: TRunStatus read FStatus write FStatus;

    /// <summary>
    /// Gets or sets the details of the last error that occurred while processing the file.
    /// </summary>
    /// <remarks>
    /// This property contains information about any errors encountered during file processing.
    /// If no errors occurred, the value is <c>null</c>.
    /// </remarks>
    property LastError: TLastError read FLastError write FLastError;

    /// <summary>
    /// Gets or sets the chunking strategy used to split the file.
    /// </summary>
    /// <remarks>
    /// This property defines the settings used to divide the file into smaller, overlapping chunks
    /// for indexing and search efficiency within the vector store.
    /// </remarks>
    property ChunkingStrategy: TChunkingStrategy read FChunkingStrategy write FChunkingStrategy;

    /// <summary>
    /// Gets or sets the set of key-value pairs attached to the file, represented as a JSON string.
    /// </summary>
    /// <remarks>
    /// Up to 16 key-value pairs can be attached to a file. Keys are strings with a maximum length of
    /// 64 characters; values are strings (max 512 chars), booleans, or numbers. Useful for filtering during searches.
    /// </remarks>
    property Attributes: string read FAttributes write FAttributes;

    destructor Destroy; override;
  end;

  /// <summary>
  /// Represents a list of files attached to a vector store in the OpenAI API.
  /// </summary>
  /// <remarks>
  /// The <c>TVectorStoreFiles</c> class is a collection of <c>TVectorStoreFile</c> objects,
  /// providing access to details about multiple files within a vector store. It supports
  /// iteration, allowing users to retrieve information about each file, such as its status,
  /// usage, and chunking strategy.
  /// </remarks>
  TVectorStoreFiles = TAdvancedList<TVectorStoreFile>;

  /// <summary>
  /// Manages asynchronous callBacks for a request using <c>TVectorStoreFile</c> as the response type.
  /// </summary>
  /// <remarks>
  /// The <c>TAsynVectorStoreFile</c> type extends the <c>TAsynParams&lt;TVectorStoreFile&gt;</c> record to handle the lifecycle of an asynchronous chat operation.
  /// It provides event handlers that trigger at various stages, such as when the operation starts, completes successfully, or encounters an error.
  /// This structure facilitates non-blocking operations.
  /// </remarks>
  TAsynVectorStoreFile = TAsynCallBack<TVectorStoreFile>;

  /// <summary>
  /// Defines a promise-based callback type for operations returning a single vector store file.
  /// </summary>
  /// <remarks>
  /// <para>
  /// <c>TPromiseVectorStoreFile</c> is an alias for <c>TPromiseCallBack&lt;TVectorStoreFile&gt;</c>,
  /// representing an asynchronous operation that resolves with a <see cref="TVectorStoreFile"/> instance.
  /// </para>
  /// <para>
  /// Use this type when you need a promise-style API for creating, retrieving,
  /// or deleting a file in a vector store.
  /// </para>
  /// </remarks>
  TPromiseVectorStoreFile = TPromiseCallBack<TVectorStoreFile>;

  /// <summary>
  /// Manages asynchronous callBacks for a request using <c>TVectorStoreFiles</c> as the response type.
  /// </summary>
  /// <remarks>
  /// The <c>TAsynVectorStoreFiles</c> type extends the <c>TAsynParams&lt;TVectorStoreFiles&gt;</c> record to handle the lifecycle of an asynchronous chat operation.
  /// It provides event handlers that trigger at various stages, such as when the operation starts, completes successfully, or encounters an error.
  /// This structure facilitates non-blocking operations.
  /// </remarks>
  TAsynVectorStoreFiles = TAsynCallBack<TVectorStoreFiles>;

  /// <summary>
  /// Defines a promise-based callback type for operations returning a collection of vector store files.
  /// </summary>
  /// <remarks>
  /// <para>
  /// <c>TPromiseVectorStoreFiles</c> is an alias for <c>TPromiseCallBack&lt;TVectorStoreFiles&gt;</c>,
  /// representing an asynchronous operation that resolves with a <see cref="TVectorStoreFiles"/> list.
  /// </para>
  /// <para>
  /// Use this type when you need a promise-style API for listing, filtering,
  /// or retrieving multiple files from a vector store.
  /// </para>
  /// </remarks>
  TPromiseVectorStoreFiles = TPromiseCallBack<TVectorStoreFiles>;

  /// <summary>
  /// Represents a single content chunk returned when retrieving the parsed contents of a vector store file.
  /// </summary>
  TVectorStoreFileContentData = class
  private
    FType: string;
    FText: string;
  public
    /// <summary>
    /// The content type, currently always <c>text</c>.
    /// </summary>
    property &Type: string read FType write FType;

    /// <summary>
    /// The text content.
    /// </summary>
    property Text: string read FText write FText;
  end;

  /// <summary>
  /// Represents a page of parsed contents of a vector store file.
  /// </summary>
  TVectorStoreFileContent = class(TJSONFingerprint)
  private
    FObject: string;
    FData: TArray<TVectorStoreFileContentData>;
    [JsonNameAttribute('has_more')]
    FHasMore: Boolean;
    [JsonNameAttribute('next_page')]
    FNextPage: string;
  public
    /// <summary>
    /// The object type, which is always <c>vector_store.file_content.page</c>.
    /// </summary>
    property &Object: string read FObject write FObject;

    /// <summary>
    /// The parsed content chunks of the file.
    /// </summary>
    property Data: TArray<TVectorStoreFileContentData> read FData write FData;

    /// <summary>
    /// Indicates whether there are more content pages to fetch.
    /// </summary>
    property HasMore: Boolean read FHasMore write FHasMore;

    /// <summary>
    /// The token for the next page of content, if any.
    /// </summary>
    property NextPage: string read FNextPage write FNextPage;

    destructor Destroy; override;
  end;

  /// <summary>
  /// Manages asynchronous callBacks for a request using <c>TVectorStoreFileContent</c> as the response type.
  /// </summary>
  TAsynVectorStoreFileContent = TAsynCallBack<TVectorStoreFileContent>;

  /// <summary>
  /// Defines a promise-based callback type for operations returning the parsed contents of a vector store file.
  /// </summary>
  TPromiseVectorStoreFileContent = TPromiseCallBack<TVectorStoreFileContent>;

  TVectorStoreFilesAbstractSupport = class(TGenAIRoute)
  protected
    function Create(const VectorStoreId: string; const ParamProc: TProc<TVectorStoreFilesCreateParams>): TVectorStoreFile; virtual; abstract;
    function List(const VectorStoreId: string): TVectorStoreFiles; overload; virtual; abstract;
    function List(const VectorStoreId: string; const ParamProc: TProc<TVectorStoreFilesUrlParams>): TVectorStoreFiles; overload; virtual; abstract;
    function Retrieve(const VectorStoreId: string; const FileId: string): TVectorStoreFile; virtual; abstract;
    function RetrieveContent(const VectorStoreId: string; const FileId: string): TVectorStoreFileContent; virtual; abstract;
    function Delete(const VectorStoreId: string; const FileId: string): TDeletion; virtual; abstract;
  end;

  TVectorStoreFilesAsynchronousSupport = class(TVectorStoreFilesAbstractSupport)
  public
    procedure AsynCreate(const VectorStoreId: string; const ParamProc: TProc<TVectorStoreFilesCreateParams>;
      const CallBacks: TFunc<TAsynVectorStoreFile>);
    procedure AsynList(const VectorStoreId: string;
      const CallBacks: TFunc<TAsynVectorStoreFiles>); overload;
    procedure AsynList(const VectorStoreId: string;
      const ParamProc: TProc<TVectorStoreFilesUrlParams>;
      const CallBacks: TFunc<TAsynVectorStoreFiles>); overload;
    procedure AsynRetrieve(const VectorStoreId: string; const FileId: string;
      const CallBacks: TFunc<TAsynVectorStoreFile>);
    procedure AsynRetrieveContent(const VectorStoreId: string; const FileId: string;
      const CallBacks: TFunc<TAsynVectorStoreFileContent>);
    procedure AsynDelete(const VectorStoreId: string; const FileId: string;
      const CallBacks: TFunc<TAsynDeletion>);
  end;

  TVectorStoreFilesRoute = class(TVectorStoreFilesAsynchronousSupport)
  protected
    /// <summary>
    /// Customizes headers for API requests related to vector store files.
    /// </summary>
    procedure HeaderCustomize; override;
  public
    /// <summary>
    /// Initiates an asynchronous request to add a file to the specified vector store and returns a promise.
    /// </summary>
    /// <param name="VectorStoreId">
    /// The unique identifier of the vector store where the file will be created.
    /// </param>
    /// <param name="ParamProc">
    /// A procedure that configures the creation parameters, such as file ID and chunking strategy,
    /// via a <see cref="TVectorStoreFilesCreateParams"/> instance.
    /// </param>
    /// <param name="CallBacks">
    /// Optional callbacks wrapped in a <see cref="TPromiseVectorStoreFile"/> for handling start,
    /// success, or error events before the promise resolves.
    /// </param>
    /// <returns>
    /// A <see cref="TPromise&lt;TVectorStoreFile&gt;"/> that resolves with the created <see cref="TVectorStoreFile"/>.
    /// </returns>
    function AsyncAwaitCreate(const VectorStoreId: string;
      const ParamProc: TProc<TVectorStoreFilesCreateParams>;
      const CallBacks: TFunc<TPromiseVectorStoreFile> = nil): TPromise<TVectorStoreFile>;

    /// <summary>
    /// Initiates an asynchronous request to retrieve all files from the specified vector store and returns a promise.
    /// </summary>
    /// <param name="VectorStoreId">
    /// The unique identifier of the vector store whose files you want to list.
    /// </param>
    /// <param name="CallBacks">
    /// Optional callbacks wrapped in a <see cref="TPromiseVectorStoreFiles"/> for handling start,
    /// success, or error events before the promise resolves.
    /// </param>
    /// <returns>
    /// A <see cref="TPromise&lt;TVectorStoreFiles&gt;"/> that resolves with a <see cref="TVectorStoreFiles"/> list.
    /// </returns>
    function AsyncAwaitList(const VectorStoreId: string;
      const CallBacks: TFunc<TPromiseVectorStoreFiles> = nil): TPromise<TVectorStoreFiles>; overload;

    /// <summary>
    /// Initiates an asynchronous request to retrieve a filtered list of files from the specified vector store and returns a promise.
    /// </summary>
    /// <param name="VectorStoreId">
    /// The unique identifier of the vector store whose files you want to list.
    /// </param>
    /// <param name="ParamProc">
    /// A procedure that configures URL parameters�such as status filters�via a <see cref="TVectorStoreFilesUrlParams"/> instance.
    /// </param>
    /// <param name="CallBacks">
    /// Optional callbacks wrapped in a <see cref="TPromiseVectorStoreFiles"/> for handling start,
    /// success, or error events before the promise resolves.
    /// </param>
    /// <returns>
    /// A <see cref="TPromise&lt;TVectorStoreFiles&gt;"/> that resolves with a filtered <see cref="TVectorStoreFiles"/> list.
    /// </returns>
    function AsyncAwaitList(const VectorStoreId: string;
      const ParamProc: TProc<TVectorStoreFilesUrlParams>;
      const CallBacks: TFunc<TPromiseVectorStoreFiles> = nil): TPromise<TVectorStoreFiles>; overload;

    /// <summary>
    /// Initiates an asynchronous request to retrieve a specific file from the specified vector store and returns a promise.
    /// </summary>
    /// <param name="VectorStoreId">
    /// The unique identifier of the vector store containing the file.
    /// </param>
    /// <param name="FileId">
    /// The unique identifier of the file to retrieve.
    /// </param>
    /// <param name="CallBacks">
    /// Optional callbacks wrapped in a <see cref="TPromiseVectorStoreFile"/> for handling start,
    /// success, or error events before the promise resolves.
    /// </param>
    /// <returns>
    /// A <see cref="TPromise&lt;TVectorStoreFile&gt;"/> that resolves with the requested <see cref="TVectorStoreFile"/>.
    /// </returns>
    function AsyncAwaitRetrieve(const VectorStoreId: string; const FileId: string;
      const CallBacks: TFunc<TPromiseVectorStoreFile> = nil): TPromise<TVectorStoreFile>;

    /// <summary>
    /// Initiates an asynchronous request to retrieve the parsed contents of a specific file from the specified vector store and returns a promise.
    /// </summary>
    /// <param name="VectorStoreId">
    /// The unique identifier of the vector store containing the file.
    /// </param>
    /// <param name="FileId">
    /// The unique identifier of the file whose contents will be retrieved.
    /// </param>
    /// <param name="CallBacks">
    /// Optional callbacks wrapped in a <see cref="TPromiseVectorStoreFileContent"/> for handling start,
    /// success, or error events before the promise resolves.
    /// </param>
    /// <returns>
    /// A <see cref="TPromise&lt;TVectorStoreFileContent&gt;"/> that resolves with the parsed contents of the file.
    /// </returns>
    function AsyncAwaitRetrieveContent(const VectorStoreId: string; const FileId: string;
      const CallBacks: TFunc<TPromiseVectorStoreFileContent> = nil): TPromise<TVectorStoreFileContent>;

    /// <summary>
    /// Initiates an asynchronous request to delete a specific file from the specified vector store and returns a promise.
    /// </summary>
    /// <param name="VectorStoreId">
    /// The unique identifier of the vector store from which the file will be deleted.
    /// </param>
    /// <param name="FileId">
    /// The unique identifier of the file to delete.
    /// </param>
    /// <param name="CallBacks">
    /// Optional callbacks wrapped in a <see cref="TPromiseDeletion"/> for handling start,
    /// success, or error events before the promise resolves.
    /// </param>
    /// <returns>
    /// A <see cref="TPromise&lt;TDeletion&gt;"/> that resolves with a <see cref="TDeletion"/> object indicating the outcome.
    /// </returns>
    function AsyncAwaitDelete(const VectorStoreId: string; const FileId: string;
      const CallBacks: TFunc<TPromiseDeletion> = nil): TPromise<TDeletion>;

    /// <summary>
    /// Synchronously creates a new file in the specified vector store.
    /// </summary>
    /// <param name="VectorStoreId">
    /// The unique identifier of the vector store where the file will be added.
    /// </param>
    /// <param name="ParamProc">
    /// A procedure that configures the parameters for creating the file, such as the file ID
    /// and chunking strategy.
    /// </param>
    /// <returns>
    /// A <c>TVectorStoreFile</c> object representing the created file.
    /// </returns>
    function Create(const VectorStoreId: string; const ParamProc: TProc<TVectorStoreFilesCreateParams>): TVectorStoreFile; override;

    /// <summary>
    /// Synchronously retrieves a list of files from a specified vector store.
    /// </summary>
    /// <param name="VectorStoreId">
    /// The unique identifier of the vector store from which to retrieve the files.
    /// </param>
    /// <returns>
    /// A <c>TVectorStoreFiles</c> list containing information about the files.
    /// </returns>
    function List(const VectorStoreId: string): TVectorStoreFiles; overload; override;

    /// <summary>
    /// Synchronously retrieves a filtered list of files from a specified vector store.
    /// </summary>
    /// <param name="VectorStoreId">
    /// The unique identifier of the vector store from which to retrieve the files.
    /// </param>
    /// <param name="ParamProc">
    /// A procedure to configure filtering parameters for the list request, such as file status.
    /// </param>
    /// <returns>
    /// A <c>TVectorStoreFiles</c> list containing information about the filtered files.
    /// </returns>
    function List(const VectorStoreId: string; const ParamProc: TProc<TVectorStoreFilesUrlParams>): TVectorStoreFiles; overload; override;

    /// <summary>
    /// Synchronously retrieves details of a specific file within a vector store.
    /// </summary>
    /// <param name="VectorStoreId">
    /// The unique identifier of the vector store containing the file.
    /// </param>
    /// <param name="FileId">
    /// The unique identifier of the file to be retrieved.
    /// </param>
    /// <returns>
    /// A <c>TVectorStoreFile</c> object containing the details of the specified file.
    /// </returns>
    function Retrieve(const VectorStoreId: string; const FileId: string): TVectorStoreFile; override;

    /// <summary>
    /// Synchronously retrieves the parsed contents of a specific file within a vector store.
    /// </summary>
    /// <param name="VectorStoreId">
    /// The unique identifier of the vector store containing the file.
    /// </param>
    /// <param name="FileId">
    /// The unique identifier of the file whose contents are to be retrieved.
    /// </param>
    /// <returns>
    /// A <c>TVectorStoreFileContent</c> object containing the parsed content chunks of the specified file.
    /// </returns>
    function RetrieveContent(const VectorStoreId: string; const FileId: string): TVectorStoreFileContent; override;

    /// <summary>
    /// Synchronously deletes a file from the specified vector store.
    /// </summary>
    /// <param name="VectorStoreId">
    /// The unique identifier of the vector store from which to delete the file.
    /// </param>
    /// <param name="FileId">
    /// The unique identifier of the file to be deleted.
    /// </param>
    /// <returns>
    /// A <c>TDeletion</c> object indicating the status of the deletion.
    /// </returns>
    function Delete(const VectorStoreId: string; const FileId: string): TDeletion; override;
  end;

implementation

uses
  System.DateUtils;

function VectorFilesUnixToUtc(const Value: Int64): string;
begin
  if Value <= 0 then
    Exit(EmptyStr);
  Result := FormatDateTime('yyyy-mm-dd"T"hh:nn:ss"Z"', UnixToDateTime(Value, True));
end;

{ TVectorStoreFilesCreateParams }

function TVectorStoreFilesCreateParams.ChunkingStrategy(
  const Value: TChunkingStrategyParams): TVectorStoreFilesCreateParams;
begin
  Result := TVectorStoreFilesCreateParams(Add('chunking_strategy', Value.Detach));
end;

function TVectorStoreFilesCreateParams.FileId(
  const Value: string): TVectorStoreFilesCreateParams;
begin
  Result := TVectorStoreFilesCreateParams(Add('file_id', Value));
end;

function TVectorStoreFilesCreateParams.Attributes(
  const Value: TJSONObject): TVectorStoreFilesCreateParams;
begin
  Result := TVectorStoreFilesCreateParams(Add('attributes', Value));
end;

{ TVectorStoreFile }

destructor TVectorStoreFile.Destroy;
begin
  if Assigned(FLastError) then
    FLastError.Free;
  if Assigned(FChunkingStrategy) then
    FChunkingStrategy.Free;
  inherited;
end;

function TVectorStoreFile.GetCreatedAt: Int64;
begin
  Result := FCreatedAt;
end;

function TVectorStoreFile.GetCreatedAtAsString: string;
begin
  Result := VectorFilesUnixToUtc(FCreatedAt);
end;

{ TChunkingStrategy }

destructor TChunkingStrategy.Destroy;
begin
  if Assigned(FStatic) then
    FStatic.Free;
  inherited;
end;

{ TVectorStoreFileContent }

destructor TVectorStoreFileContent.Destroy;
begin
  for var Item in FData do
    Item.Free;
  inherited;
end;

{ TVectorStoreFilesAsynchronousSupport }

procedure TVectorStoreFilesAsynchronousSupport.AsynCreate(const VectorStoreId: string;
  const ParamProc: TProc<TVectorStoreFilesCreateParams>;
  const CallBacks: TFunc<TAsynVectorStoreFile>);
begin
  with TAsynCallBackExec<TAsynVectorStoreFile, TVectorStoreFile>.Create(CallBacks) do
  try
    Sender := Use.Param.Sender;
    OnStart := Use.Param.OnStart;
    OnSuccess := Use.Param.OnSuccess;
    OnError := Use.Param.OnError;
    Run(
      function: TVectorStoreFile
      begin
        Result := Self.Create(VectorStoreId, ParamProc);
      end);
  finally
    Free;
  end;
end;

procedure TVectorStoreFilesAsynchronousSupport.AsynDelete(const VectorStoreId, FileId: string;
  const CallBacks: TFunc<TAsynDeletion>);
begin
  with TAsynCallBackExec<TAsynDeletion, TDeletion>.Create(CallBacks) do
  try
    Sender := Use.Param.Sender;
    OnStart := Use.Param.OnStart;
    OnSuccess := Use.Param.OnSuccess;
    OnError := Use.Param.OnError;
    Run(
      function: TDeletion
      begin
        Result := Self.Delete(VectorStoreId, FileId);
      end);
  finally
    Free;
  end;
end;

procedure TVectorStoreFilesAsynchronousSupport.AsynList(const VectorStoreId: string;
  const CallBacks: TFunc<TAsynVectorStoreFiles>);
begin
  with TAsynCallBackExec<TAsynVectorStoreFiles, TVectorStoreFiles>.Create(CallBacks) do
  try
    Sender := Use.Param.Sender;
    OnStart := Use.Param.OnStart;
    OnSuccess := Use.Param.OnSuccess;
    OnError := Use.Param.OnError;
    Run(
      function: TVectorStoreFiles
      begin
        Result := Self.List(VectorStoreId);
      end);
  finally
    Free;
  end;
end;

procedure TVectorStoreFilesAsynchronousSupport.AsynList(const VectorStoreId: string;
  const ParamProc: TProc<TVectorStoreFilesUrlParams>;
  const CallBacks: TFunc<TAsynVectorStoreFiles>);
begin
  with TAsynCallBackExec<TAsynVectorStoreFiles, TVectorStoreFiles>.Create(CallBacks) do
  try
    Sender := Use.Param.Sender;
    OnStart := Use.Param.OnStart;
    OnSuccess := Use.Param.OnSuccess;
    OnError := Use.Param.OnError;
    Run(
      function: TVectorStoreFiles
      begin
        Result := Self.List(VectorStoreId, ParamProc);
      end);
  finally
    Free;
  end;
end;

procedure TVectorStoreFilesAsynchronousSupport.AsynRetrieve(const VectorStoreId,
  FileId: string; const CallBacks: TFunc<TAsynVectorStoreFile>);
begin
  with TAsynCallBackExec<TAsynVectorStoreFile, TVectorStoreFile>.Create(CallBacks) do
  try
    Sender := Use.Param.Sender;
    OnStart := Use.Param.OnStart;
    OnSuccess := Use.Param.OnSuccess;
    OnError := Use.Param.OnError;
    Run(
      function: TVectorStoreFile
      begin
        Result := Self.Retrieve(VectorStoreId, FileId);
      end);
  finally
    Free;
  end;
end;

procedure TVectorStoreFilesAsynchronousSupport.AsynRetrieveContent(const VectorStoreId,
  FileId: string; const CallBacks: TFunc<TAsynVectorStoreFileContent>);
begin
  with TAsynCallBackExec<TAsynVectorStoreFileContent, TVectorStoreFileContent>.Create(CallBacks) do
  try
    Sender := Use.Param.Sender;
    OnStart := Use.Param.OnStart;
    OnSuccess := Use.Param.OnSuccess;
    OnError := Use.Param.OnError;
    Run(
      function: TVectorStoreFileContent
      begin
        Result := Self.RetrieveContent(VectorStoreId, FileId);
      end);
  finally
    Free;
  end;
end;

{ TVectorStoreFilesRoute }

function TVectorStoreFilesRoute.AsyncAwaitCreate(const VectorStoreId: string;
  const ParamProc: TProc<TVectorStoreFilesCreateParams>;
  const CallBacks: TFunc<TPromiseVectorStoreFile>): TPromise<TVectorStoreFile>;
begin
  Result := TAsyncAwaitHelper.WrapAsyncAwait<TVectorStoreFile>(
    procedure(const CallBackParams: TFunc<TAsynVectorStoreFile>)
    begin
      AsynCreate(VectorStoreId, ParamProc, CallBackParams);
    end,
    CallBacks);
end;

function TVectorStoreFilesRoute.AsyncAwaitDelete(const VectorStoreId,
  FileId: string;
  const CallBacks: TFunc<TPromiseDeletion>): TPromise<TDeletion>;
begin
  Result := TAsyncAwaitHelper.WrapAsyncAwait<TDeletion>(
    procedure(const CallBackParams: TFunc<TAsynDeletion>)
    begin
      AsynDelete(VectorStoreId, FileId, CallBackParams);
    end,
    CallBacks);
end;

function TVectorStoreFilesRoute.AsyncAwaitList(const VectorStoreId: string;
  const ParamProc: TProc<TVectorStoreFilesUrlParams>;
  const CallBacks: TFunc<TPromiseVectorStoreFiles>): TPromise<TVectorStoreFiles>;
begin
  Result := TAsyncAwaitHelper.WrapAsyncAwait<TVectorStoreFiles>(
    procedure(const CallBackParams: TFunc<TAsynVectorStoreFiles>)
    begin
      AsynList(VectorStoreId, ParamProc, CallBackParams);
    end,
    CallBacks);
end;

function TVectorStoreFilesRoute.AsyncAwaitList(const VectorStoreId: string;
  const CallBacks: TFunc<TPromiseVectorStoreFiles>): TPromise<TVectorStoreFiles>;
begin
  Result := TAsyncAwaitHelper.WrapAsyncAwait<TVectorStoreFiles>(
    procedure(const CallBackParams: TFunc<TAsynVectorStoreFiles>)
    begin
      AsynList(VectorStoreId, CallBackParams);
    end,
    CallBacks);
end;

function TVectorStoreFilesRoute.AsyncAwaitRetrieve(const VectorStoreId,
  FileId: string;
  const CallBacks: TFunc<TPromiseVectorStoreFile>): TPromise<TVectorStoreFile>;
begin
  Result := TAsyncAwaitHelper.WrapAsyncAwait<TVectorStoreFile>(
    procedure(const CallBackParams: TFunc<TAsynVectorStoreFile>)
    begin
      AsynRetrieve(VectorStoreId, FileId, CallBackParams);
    end,
    CallBacks);
end;

function TVectorStoreFilesRoute.AsyncAwaitRetrieveContent(const VectorStoreId,
  FileId: string;
  const CallBacks: TFunc<TPromiseVectorStoreFileContent>): TPromise<TVectorStoreFileContent>;
begin
  Result := TAsyncAwaitHelper.WrapAsyncAwait<TVectorStoreFileContent>(
    procedure(const CallBackParams: TFunc<TAsynVectorStoreFileContent>)
    begin
      AsynRetrieveContent(VectorStoreId, FileId, CallBackParams);
    end,
    CallBacks);
end;

function TVectorStoreFilesRoute.Create(const VectorStoreId: string;
  const ParamProc: TProc<TVectorStoreFilesCreateParams>): TVectorStoreFile;
begin
  HeaderCustomize;
  Result := API.Post<TVectorStoreFile, TVectorStoreFilesCreateParams>('vector_stores/' + VectorStoreId + '/files', ParamProc);
end;

function TVectorStoreFilesRoute.Delete(const VectorStoreId,
  FileId: string): TDeletion;
begin
  HeaderCustomize;
  Result := API.Delete<TDeletion>('vector_stores/' + VectorStoreId + '/files/' + FileId);
end;

procedure TVectorStoreFilesRoute.HeaderCustomize;
begin
  inherited;
  API.CustomHeaders := [TNetHeader.Create('OpenAI-Beta', 'assistants=v2')];
end;

function TVectorStoreFilesRoute.List(
  const VectorStoreId: string): TVectorStoreFiles;
begin
  HeaderCustomize;
  Result := API.Get<TVectorStoreFiles>('vector_stores/' + VectorStoreId + '/files');
end;

function TVectorStoreFilesRoute.List(const VectorStoreId: string;
  const ParamProc: TProc<TVectorStoreFilesUrlParams>): TVectorStoreFiles;
begin
  HeaderCustomize;
  Result := API.Get<TVectorStoreFiles, TVectorStoreFilesUrlParams>('vector_stores/' + VectorStoreId + '/files', ParamProc);
end;

function TVectorStoreFilesRoute.Retrieve(const VectorStoreId,
  FileId: string): TVectorStoreFile;
begin
  HeaderCustomize;
  Result := API.Get<TVectorStoreFile>('vector_stores/' + VectorStoreId + '/files/' + FileId);
end;

function TVectorStoreFilesRoute.RetrieveContent(const VectorStoreId,
  FileId: string): TVectorStoreFileContent;
begin
  HeaderCustomize;
  Result := API.Get<TVectorStoreFileContent>('vector_stores/' + VectorStoreId + '/files/' + FileId + '/content');
end;

{ TVectorStoreFilesUrlParams }

function TVectorStoreFilesUrlParams.Filter(
  const Value: string): TVectorStoreFilesUrlParams;
begin
  Result := TVectorStoreFilesUrlParams(Add('filter', Value));
end;

end.
